plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.9'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "org.sonarqube" version "3.5.0.2730"
	id 'jacoco'
}

group = 'ourinhos.fatec'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

List<String> excludeAnalysisPaths = List.of(
		"**/config/**",
		"**/exception/**",
		"**/*Application.*"
)

sonarqube {
	properties {
		property "sonar.projectKey", "fatec-ourinhos-centralestagio_Det"
		property "sonar.organization", "centralestagio"
		property "sonar.host.url", "https://sonarcloud.io"
		property("sonar.exclusions", excludeAnalysisPaths)
		property("sonar.jacoco.reportPaths", "$buildDir/jacoco/*.exec")
		property("sonar.junit.reportPaths", "$buildDir/test-results/test/")
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
	implementation 'org.postgresql:postgresql'
	implementation 'org.springframework.security:spring-security-core:5.5.1'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security:2.6.4'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation 'org.projectlombok:lombok:1.18.22'

	implementation 'io.springfox:springfox-swagger2:3.0.0'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	compileOnly 'org.projectlombok:lombok:1.18.4'
	annotationProcessor 'org.projectlombok:lombok:1.18.4'
}

jacoco {
	toolVersion = "0.8.7"
	reportsDirectory.set(layout.buildDirectory.dir("reports/jacoco/codeCoverageReport"))
}

jacocoTestReport {
	reports {
		html.required.set(true)
		xml.required.set(true)
		csv.required.set(false)
	}
	classDirectories.setFrom(
			files(classDirectories.files.collect{
				fileTree(it) {
					exclude(excludeAnalysisPaths)
				}
			})
	)
}

tasks.named('test') {
	it.useJUnitPlatform()
	finalizedBy(jacocoTestReport)
}
